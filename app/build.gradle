plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'com.google.dagger.hilt.android'
    id 'idea'
    id 'org.jetbrains.kotlin.plugin.serialization' version "$kotlin_version"
}

idea.module {
    excludeDirs.add(file("libraries/boost"))
}

project.ext.isBuildSigned = (System.getenv("CI") == "true") && (System.getenv("IS_BUILD_SIGNED") == "true")

android {
    namespace 'org.sphereemu.sphere'
    compileSdk 34

    defaultConfig {
        applicationId "org.sphereemu.sphere"

        minSdk 29
        targetSdk 33

        versionCode getGitVersionCode()
        versionName getGitVersionName()

        ndk {
            abiFilters "arm64-v8a"
        }

        manifestPlaceholders["emulationProcess"] = ""

        def locales = ["en", "de", "el", "es", "es-419", "fr", "hu", "id", "it", "ja", "ko", "pl", "ru", "ta", "zh-Hans", "zh-Hant"]

        buildConfigField "String[]", "AVAILABLE_APP_LANGUAGES", "new String[]{\"" + locales.join("\",\"") + "\"}"
    }

    def javaVersion = JavaVersion.VERSION_17
    kotlinOptions {
        jvmTarget = javaVersion.toString()
        freeCompilerArgs += "-opt-in=kotlin.RequiresOptIn"
    }
    compileOptions {
        sourceCompatibility javaVersion
        targetCompatibility javaVersion
    }

    packagingOptions {
        jniLibs.useLegacyPackaging = true
    }

    signingConfigs {
        ci {
            storeFile file(System.getenv("SIGNING_STORE_PATH") ?: "${System.getenv("user.home")}/keystore.jks")
            storePassword System.getenv("SIGNING_STORE_PASSWORD")
            keyAlias System.getenv("SIGNING_KEY_ALIAS")
            keyPassword System.getenv("SIGNING_KEY_PASSWORD")
        }
    }

    buildTypes {
        release {
            debuggable true
            externalNativeBuild {
                cmake {
                    arguments "-DCMAKE_BUILD_TYPE=RELEASE"
                }
            }
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig = isBuildSigned ? signingConfigs.ci : signingConfigs.debug
            manifestPlaceholders["emulationProcess"] = ":emulationProcess"
        }

        reldebug {
            debuggable true
            externalNativeBuild {
                cmake {
                    arguments "-DCMAKE_BUILD_TYPE=RELWITHDEBINFO"
                }
            }
            minifyEnabled false
            shrinkResources false
            signingConfig = isBuildSigned ? signingConfigs.ci : signingConfigs.debug
        }

        debug {
            debuggable true
            minifyEnabled false
            shrinkResources false
            signingConfig = isBuildSigned ? signingConfigs.ci : signingConfigs.debug
        }
    }

    flavorDimensions += "version"
    productFlavors {
        mainline {
            dimension = "version"
        }

        dev {
            dimension = "version"
            applicationIdSuffix = ".dev"
        }
    }

    buildFeatures {
        viewBinding true
        buildConfig true
    }

    ndkVersion '26.1.10909125'
    externalNativeBuild {
        cmake {
            version '3.22.1+'
            path "CMakeLists.txt"
        }
    }

    androidResources {
        ignoreAssetsPattern '*.md'
    }

    sourceSets {
        reldebug {
            jniLibs {
                srcDir "libraries/vklayers"
            }
        }

        debug {
            jniLibs {
                srcDir "libraries/vklayers"
            }
        }
    }
}

dependencies {
    implementation 'androidx.core:core-ktx:1.12.0'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.preference:preference-ktx:1.2.1'
    implementation 'androidx.activity:activity-ktx:1.8.2'
    implementation 'com.google.android.material:material:1.10.0'
    implementation 'androidx.documentfile:documentfile:1.0.1'
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'
    implementation 'androidx.window:window:1.2.0'
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.6.2"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:2.6.2"
    implementation 'androidx.fragment:fragment-ktx:1.6.2'
    implementation "com.google.dagger:hilt-android:$hilt_version"
    kapt "com.google.dagger:hilt-compiler:$hilt_version"
    implementation 'com.google.android.flexbox:flexbox:3.0.0'

    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    implementation 'org.jetbrains.kotlinx:kotlinx-serialization-json:1.6.2'

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    implementation 'info.debatty:java-string-similarity:2.0.0'
    implementation 'com.github.KikiManjaro:colorpicker:v1.1.12'
    implementation 'com.github.android:renderscript-intrinsics-replacement-toolkit:344be3f'
}

kapt {
    correctErrorTypes true
}

def getGitVersionName() {
    def versionName = '0.0.0'

    if (System.getenv("CI") != "true") {
        def process = 'git describe --exact-match'.execute([], project.rootDir)
        def isTag = process.waitFor() == 0

        def tag = 'git describe --abbrev=0'.execute([], project.rootDir).text.trim()
        if (!tag.isEmpty())
            versionName = tag

        if (!isTag)
            versionName += '-' + getGitBranch() + '-' + getGitShortHash()
    }

    return versionName
}

def getGitVersionCode() {
    def versionCode = 1

    if (System.getenv("CI") != "true") {
        versionCode = Integer.max('git rev-list --first-parent --count --tags'.execute([], project.rootDir).text.toInteger(), versionCode)
    }

    return versionCode
}

def getGitShortHash() {
    def gitHash = '0'

    if (System.getenv("CI") != "true") {
        gitHash = 'git rev-parse --short HEAD'.execute([], project.rootDir).text.trim()
    }

    return gitHash
}

def getGitBranch() {
    def branch = 'unk'

    if (System.getenv("CI") != "true") {
        branch = 'git rev-parse --abbrev-ref HEAD'.execute([], project.rootDir).text.trim()
    }

    return branch
}
